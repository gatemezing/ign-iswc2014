PREFIX geofla: <http://data.ign.fr/def/geofla#>
PREFIX geom: <http://data.ign.fr/def/geometrie#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gspql: <http://www.opengis.net/ont/geosparql#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {  
 ?x a geofla:Departement.
 ?x geofla:codeDpt ?val1.
 ?x rdfs:label ?val2.
 ?x geom:geometry ?w.
 ?x geofla:region ?val4.
 ?x geofla:chefLieu ?val5.
 ?x geofla:siegeDuChefLieu ?y.
 ?y a geom:Point.
 ?y geom:coordX ?valXCHF.
 ?y geom:coordY ?valYCHF.
 ?y geom:crs ?val9.
 ?y gspql:asWKT ?wktChf.
 ?w a geom:MultiPolygon.
 ?w gspql:asWKT ?val10.
 ?w geom:crs ?val11.
 ?w geom:centroid ?z.
 ?z a geom:Point.
 ?z geom:coordX ?valXCTR.
 ?z geom:coordY ?valYCTR.
 ?z geom:crs ?val14.
 ?z gspql:asWKT ?wktCtr
  }

 FROM <http://data.ign.fr/id/geofla/departement-rdf-1>
 
WHERE {
 ?x a geofla:Departement.
 ?x geofla:codeDpt ?val1.
 ?x rdfs:label ?val2.
 ?x geom:geometry ?w.
 ?x geofla:region ?val4.
 ?x geofla:chefLieu ?val5.
 ?x geofla:siegeDuChefLieu ?y.
 ?y a geom:Point.
 ?y geom:coordX ?val7.
 ?y geom:coordY ?val8.
 ?y geom:crs ?val9.
 ?w a geom:MultiPolygon.
 ?w gspql:asWKT ?val10.
 ?w geom:crs ?val11.
 ?w geom:centroid ?z.
 ?z a geom:Point.
 ?z geom:coordX ?val12. 
 ?z geom:coordY ?val13.
 ?z geom:crs ?val14.
   BIND (STRDT ( STR (?val7), xsd:double) AS ?valXCHF).
   BIND (STRDT ( STR (?val8), xsd:double) AS ?valYCHF).
   BIND (STRDT ( STR (?val12), xsd:double) AS ?valXCTR).
   BIND (STRDT ( STR (?val13), xsd:double) AS ?valYCTR).
  BIND (STRDT (CONCAT( "<http://data.ign.fr/id/ignf/crs/RGF93LAMB93> POINT(",STR (?val7)," ",STR (?val8),")"), gspql:wktLiteral) AS ?wktChf).
  BIND (STRDT (CONCAT( "<http://data.ign.fr/id/ignf/crs/RGF93LAMB93> POINT(",STR (?val12)," ",STR (?val13),")"), gspql:wktLiteral) AS ?wktCtr)
 }
 
 