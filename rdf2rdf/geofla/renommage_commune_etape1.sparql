PREFIX geofla: <http://data.ign.fr/def/geofla#>
PREFIX geom: <http://data.ign.fr/def/geometrie#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gspql: <http://www.opengis.net/ont/geosparql#>

CONSTRUCT {  
 ?x2 a geofla:Commune.
 ?x2 geofla:codeComm ?code.
 ?x2 geofla:numInsee ?val2.
 ?x2 rdfs:label ?toponyme.
 ?x2 geom:geometry ?val3.
 ?x2 geofla:statut ?val17.
 ?x2 geofla:zMoyen ?zMoy.
 ?x2 geofla:superficieHa ?supHa.
 ?x2 geofla:population ?pop.
 ?x2 geofla:dpt ?dpt.
 ?x2 geofla:region ?reg.
 ?x2 geofla:arr ?arr.
 ?x2 geofla:cant ?cant.
 ?val3 a geom:MultiPolygon.
 ?val3 gspql:asWKT ?wkt.
 ?val3 geom:crs <http://data.ign.fr/id/ignf/crs/WGS84GDD>.
 ?x2 geofla:siegeDuChefLieu ?y.
 ?y a geom:Point.
 ?y geom:coordX ?valXCHF.
 ?y geom:coordY ?valYCHF.
 ?y geom:crs <http://data.ign.fr/id/ignf/crs/RGF93LAMB93>.
 ?val3 geom:centroid ?z.
 ?z a geom:Point.
 ?z geom:coordX ?valXCTR.
 ?z geom:coordY ?valYCTR.
 ?z geom:crs <http://data.ign.fr/id/ignf/crs/RGF93LAMB93>

}

 FROM <http://data.ign.fr/id/geofla/commune>
 
WHERE {
 ?x a geofla:Commune.
 ?x <http://data.ign.fr/id/geofla/commune#CODE_COMM> ?val1.
 ?x <http://data.ign.fr/id/geofla/commune#INSEE_COM> ?val2.
 ?x <http://data.ign.fr/id/geofla/commune#NOM_COMM> ?val16.
 ?x <http://data.ign.fr/ontology/geometrie#Geometrie> ?val3.
 ?val3 a ?val4.
 ?val3 gspql:asWKT ?val5.
 ?val3 <http://data.ign.fr/ontology/geometrie#systCoord> ?val6.
 ?x <http://data.ign.fr/id/geofla/commune#X_CHF_LIEU> ?val8.
 ?x <http://data.ign.fr/id/geofla/commune#Y_CHF_LIEU> ?val9.
 ?x <http://data.ign.fr/id/geofla/commune#X_CENTROID> ?val10.
 ?x <http://data.ign.fr/id/geofla/commune#Y_CENTROID> ?val11.
 ?x <http://data.ign.fr/id/geofla/commune#CODE_CANT> ?val15.
 ?x <http://data.ign.fr/id/geofla/commune#CODE_ARR> ?val14.
 ?x <http://data.ign.fr/id/geofla/commune#CODE_DEPT> ?val13.
 ?x <http://data.ign.fr/id/geofla/commune#CODE_REG> ?val12.
 ?x <http://data.ign.fr/id/geofla/commune#STATUT> ?val17.
 ?x <http://data.ign.fr/id/geofla/commune#Z_MOYEN> ?val18.
 ?x <http://data.ign.fr/id/geofla/commune#SUPERFICIE> ?val19.
 ?x <http://data.ign.fr/id/geofla/commune#POPULATION> ?val20.
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/commune/",?val2)) AS ?x2).
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/region/",?val12)) AS ?reg).
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/departement/",?val13)) AS ?dpt).
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/arrondissement/",?val13,?val14)) AS ?arr).
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/canton/",?val13,?val15)) AS ?cant).
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/commune/PointChf_",?val2)) AS ?y).
  BIND (IRI(CONCAT("http://data.ign.fr/id/geofla/commune/PointCtr_",?val2)) AS ?z).
  BIND (STR(?val1) AS ?code).
  BIND ((strlang(?val16, "fr")) AS ?toponyme).
  BIND (STRDT ( STR (?val18), xsd:integer) AS ?zMoy).
  BIND (STRDT ( STR (?val19), xsd:integer) AS ?supHa).
  BIND (STRDT ( STR (?val20), xsd:double) AS ?pop).
  BIND ((?val8*100) AS ?valXCHF).
  BIND ((?val9*100)AS ?valYCHF).
  BIND ((?val10*100)AS ?valXCTR).
  BIND ((?val11*100)AS ?valYCTR).
  BIND (strdt(CONCAT( "<http://data.ign.fr/id/ignf/crs/WGS84GDD> ",?val5),gspql:wktLiteral) AS ?wkt)

 }