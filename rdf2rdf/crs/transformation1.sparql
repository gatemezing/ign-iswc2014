prefix gmx: <http://www.isotc211.org/2005/gmx#>
prefix gml: <http://www.opengis.net/gml#>
prefix ignf: <http://data.ign.fr/def/ignf#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX qudt: <http://qudt.org/schema/qudt#>
PREFIX unit: <http://qudt.org/vocab/unit#>
prefix gmd: <http://www.isotc211.org/2005/gmd#>
prefix gco: <http://www.isotc211.org/2005/gco#>

CONSTRUCT 
{    											
	?uri a ignf:Transformation;
	rdfs:label ?namefr;
	ignf:scope ?scopefr;
	ignf:epsgID ?valepsg;
	ignf:usesMethod ?uriMthd;
	ignf:sourceCRS ?uriSrcCRS;
	ignf:targetCRS ?uriTgtCRS;
	gml:usesValue ?val.
	
	?uri ignf:domainOfValidity ?uriExt.
        ?uriExt a ignf:Extent; rdfs:label ?ldescfr; ignf:geographicElement ?uriBB. ?uriBB a ignf:GeographicBoundingBox;
        ignf:eastBoundLongitude ?vebld; 
        ignf:westBoundLongitude ?vwbld ; 
        ignf:southBoundLatitude ?vsbld ; 
	ignf:northBoundLatitude ?vnbld .

	?val gml:ParameterValue ?prmtv.
	?prmtv gml:value ?prmtv2.
	?prmtv gml:valueOfParameter ?voprmt.
	?prmtv2 <http://www.w3.org/1999/02/22-rdf-syntax-ns#value> ?prmtv3.
	?prmtv2 gml:uom ?uom.
	?voprmt <http://www.w3.org/1999/xlink#href> ?vop. 
	
	
  }
  
FROM <http://data.ign.fr/id/ignf>
WHERE 
 
{
   ?t gml:Transformation ?transfo.
   {
     SELECT DISTINCT ?name 
      WHERE {?transfo gml:name ?name. }}
   {
 
    ?transfo gml:id ?id; gml:scope ?scope ; gml:usesMethod ?m; gml:usesValue ?val; gml:operationVersion ?v; gml:sourceCRS ?src; gml:targetCRS ?tgt.
    OPTIONAL{?transfo gml:name [ gml:codeSpace ?vn; <http://www.w3.org/1999/02/22-rdf-syntax-ns#value> ?valepsg].}
	?m <http://www.w3.org/1999/xlink#href> ?mthd.
	?src <http://www.w3.org/1999/xlink#href> ?srcCRS.
	?tgt <http://www.w3.org/1999/xlink#href> ?tgtCRS.
	
	?transfo gml:domainOfValidity ?domainURI.
	?domainURI gmd:EX_Extent ?g . ?g gmd:geographicElement ?gElt. ?g gmd:description ?dURI. ?dURI gco:CharacterString ?ldesc.
		?gElt gmd:EX_GeographicBoundingBox ?bbox. 
		?bbox gmd:eastBoundLongitude ?ebl; gmd:westBoundLongitude ?wbl ; gmd:southBoundLatitude ?sbl ; gmd:northBoundLatitude ?nbl .
		?ebl gco:Decimal ?vebl. ?wbl gco:Decimal ?vwbl . ?sbl gco:Decimal ?vsbl . ?nbl gco:Decimal ?vnbl .
	
	?val gml:ParameterValue ?prmtv.
	?prmtv gml:value ?prmtv2.
	?prmtv gml:valueOfParameter ?voprmt.
	?prmtv2 <http://www.w3.org/1999/02/22-rdf-syntax-ns#value> ?prmtv3.
	?prmtv2 gml:uom ?uom.
	?voprmt <http://www.w3.org/1999/xlink#href> ?vop.
	
	BIND (IRI(CONCAT("http://data.ign.fr/id/ignf/transformation/",(str(?id)))) AS ?uri).
	BIND (IRI(CONCAT("http://data.ign.fr/id/ignf/method/",(STRAFTER(STR(?mthd),"#")))) AS ?uriMthd).
	BIND (IRI(CONCAT("http://data.ign.fr/id/ignf/crs/",(STRAFTER(STR(?srcCRS),"#")))) AS ?uriSrcCRS).
	BIND (IRI(CONCAT("http://data.ign.fr/id/ignf/crs/",(STRAFTER(STR(?tgtCRS),"#")))) AS ?uriTgtCRS).
	BIND (IRI(CONCAT("http://data.ign.fr/id/ignf/extent/",(str(?id)))) AS ?uriExt).
	BIND (IRI(CONCAT("http://data.ign.fr/id/ignf/geobbox/",(str(?id)))) AS ?uriBB).
	bind (strlang(?scope, 'fr') AS ?scopefr).
	bind (strlang(?ldesc, 'fr') AS ?ldescfr).
    bind (strdt(?vebl, xsd:double) as ?vebld).
    bind (strdt(?vwbl, xsd:double) as ?vwbld).
    bind (strdt(?vsbl, xsd:double) as ?vsbld).
    bind (strdt(?vnbl, xsd:double) as ?vnbld)
   }
      bind (strlang(?name, 'fr') AS ?namefr).

}  